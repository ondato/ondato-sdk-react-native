buildscript {
  ext.getExtOrDefault = { name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['OndatoSdkReactNative_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["OndatoSdkReactNative_" + name]).toInteger()
}

android {
  namespace "com.ondato"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // SDK core - required in all cases
  implementation("com.kyc.ondato:sdk-core:3.2.1") {
    // Current bundled version causes java.lang.NoClassDefFoundError
    exclude group: "com.squareup.okhttp3"
  }
  // Required by Ondato SDK
  implementation "com.squareup.okhttp3:logging-interceptor:4.12.0"
}

// This block injects additional Maven repositories into the root project.
// It ensures that the end-user app can resolve required native artifacts
// without having to modify its own build.gradle.
//
// Exactly one source should be active at a time (local or remote),
// depending on how the SDK is distributed for the given release.
afterEvaluate {
  rootProject.allprojects {
    repositories {
      // Local repo bundled with this React Native package (used in npm installs)
      // maven {
      //   url "$rootDir/../node_modules/ondato-sdk-react-native/android/local/repo"
      // }

      // Remote repo hosting Ondato SDK v3 artifacts
      // List of all versions for manual viewing: https://gitlab.com/ondato/ondato-sdk-android/-/packages
      maven { url "https://gitlab.com/api/v4/projects/65297321/packages/maven" }

      // Remote repo hosting Ondato SDK v2 and Facetec SDK artifacts
      maven { url "https://raw.githubusercontent.com/ondato/ondato-sdk-android/main/repos" }
    }
  }
}

